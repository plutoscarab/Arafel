Postfix ::= SUPERSCRIPT
IfThen ::= 'if' Expr 'then' Expr 'else' Expr
Case ::= Pattern '->' Expr
Matches ::= 'case' Expr (Case)+ ('otherwise' Expr)?
Pattern ::= ID ('(' Pattern (',' Pattern)* ')')? | NAT | STRING
Lambda ::= '(' Pattern ')' '=' Expr
Atom ::= NAT | STRING | OPERATOR | Lambda | '(' Expr ')' | ID | Matches | IfThen
MonoType ::= Lexpr (('->' | '→') Lexpr)*
PolyType ::= (('forall' | '∀') (ID)+ ',')? MonoType ('|' MonoType)*
TypeDecl ::= 'type' ID ('(' ID (',' ID)* ')')? '=' PolyType
LexprName ::= ID | OPERATOR
Lexpr ::= LexprName ('(' Lexpr (',' Lexpr)* ')')?
LetDecl ::= 'let' Lexpr '=' Expr
Prelude ::= LetDecl | TypeDecl
Expr ::= (Prelude)* Atom ('(' Expr (',' Expr)* ')')? (Postfix)*
